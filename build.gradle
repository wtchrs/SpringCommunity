plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.1'
    id 'io.spring.dependency-management' version '1.1.0'
}

sourceCompatibility = '17'

allprojects {
    group = 'wtchrs.SpringCommunity'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply { plugin "java" }
    apply { plugin "org.springframework.boot" }
    apply { plugin "io.spring.dependency-management" }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-web'
//        implementation 'org.springframework.boot:spring-boot-starter-security'
        developmentOnly 'org.springframework.boot:spring-boot-devtools'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'

        implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
        runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
        runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        runtimeOnly 'com.mysql:mysql-connector-j'

    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

project(":common") {
    bootJar { enabled false }
    jar { enabled true }
    dependencies {
        implementation "org.springframework.boot:spring-boot-starter-validation"
    }
}

project(":api") {
    dependencies {
        implementation project(":common")
    }
}

project(":view") {
    dependencies {
        implementation project(":common")
        implementation project(":api")
        implementation "org.springframework.boot:spring-boot-starter-thymeleaf"
        implementation 'org.springframework.boot:spring-boot-starter-webflux'
    }
}
